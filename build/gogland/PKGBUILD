pkgname=gogland
pkgver=163.12024.32
pkgrel=1
pkgdesc="Capable and Ergonomic Go IDE"
arch=("i686" "x86_64")
backup=("opt/gogland/bin/idea.properties")
options=(!strip)
url="https://www.jetbrains.com/go/"
license=(custom)
depends=("java-environment>=8" "java-runtime-common")
source=(
	"http://download.jetbrains.com/go/gogland-$pkgver.tar.gz"
	"jetbrains-gogland.desktop"
)
sha512sums=(
	"138771e19c144829bed7d2ec4ed35befaae77781cf7ac47cce970887096ce33df950e39441447c91eb9946a7f508b844d3a315b877af3c85d407e03cf3c8daa2"
	"6fefd70637118f476c84a788679cf839eff4cc133889d90adadfcce11b5ce37e6baca6355c7758f9cafaafae09ccc02d48a4ced79200cd40718664e1b63aa401"
)

package() {
	install --directory \
		$pkgdir/opt/gogland \
		$pkgdir/usr/bin \
		$pkgdir/usr/share/applications/ \
		$pkgdir/usr/share/pixmaps/

	cp --force --recursive $srcdir/Gogland-$pkgver/* $pkgdir/opt/gogland

	rm --force --recursive $pkgdir/opt/gogland/jre

	ln --symbolic /opt/gogland/bin/gogland.sh $pkgdir/usr/bin/gogland

	chmod +x $pkgdir/opt/gogland/bin/dlv

	install -D --mode=644 $srcdir/jetbrains-gogland.desktop $pkgdir/usr/share/applications/
	install -D --mode=644 $pkgdir/opt/gogland/bin/gogland.png $pkgdir/usr/share/pixmaps/gogland.png
}
